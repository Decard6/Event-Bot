plugins {
    id 'java'
    id 'com.github.johnrengelman.shadow' version '5.1.0'
}

group 'decard6.github.com'
version '2.0'

task stage(dependsOn: ['build', 'clean', 'shadowJar']){
    shadowJar.mustRunAfter clean
    build.mustRunAfter shadowJar
}


task myRun(type: JavaExec) {
    classpath sourceSets.main.runtimeClasspath
    main = "jda.Main"
}

sourceCompatibility = 1.8

repositories {
    mavenCentral()
    jcenter()
}

dependencies {
    testCompile group: 'junit', name: 'junit', version: '4.12'
    compile 'net.dv8tion:JDA:4.1.1_157'
    compile group: 'commons-collections', name: 'commons-collections', version: '3.2'
    compile 'com.vdurmont:emoji-java:5.1.1'
    compile 'org.hibernate:hibernate-core:5.4.17.Final'
    implementation 'mysql:mysql-connector-java:8.0.11'
}

def db_user = System.getenv("DB_USER")
def db_url = System.getenv('DB_URL')
def db_dialect = System.getenv('DB_DIALECT')
def db_password = System.getenv('DB_PASSWORD')

import org.apache.tools.ant.filters.ReplaceTokens

processResources {
    filesMatching(['**/*.xml', '**/*.properties', '**/*.json']) {
        filter ReplaceTokens, tokens: [
                'db_user': db_user,
                'db_password' : db_password,
                'db_url': db_url,
                'db_dialect': db_dialect,
        ]
    }
}


shadowJar{
    manifest {
        attributes 'Implementation-Title': 'EventBot', 'Implementation-Version': version, 'Main-Class': 'jda.Main'
    }
    baseName = 'EventBot'
    classifier = null
    version = null
    destinationDir = project.rootDir
}

